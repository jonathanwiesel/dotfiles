# ---- Environment set by OS X personal provision ----
# taken from https://github.com/albertogg/dotfiles/blob/master/zsh/zshrc.symlink

# Path to this dotfiles is $ZSH
export ZSH=$HOME/.dotfiles/zsh

# Get all the .zsh files
typeset -U config_files
config_files=($ZSH/*.zsh)

# Load all path.zsh files
for file in ${(M)config_files:#*/exports.zsh}
do
  source $file
done

# Load all .zsh files except path and completion files
for file in ${${config_files:#*/exports.zsh}:#*/completions.zsh}
do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completions.zsh}
do
  source $file
done

unset config_files

# Load theme
source $ZSH/*.zsh-theme

# Local config
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

export PATH="$PATH:$HOME/.rvm/bin" # Add RVM to PATH for scripting
